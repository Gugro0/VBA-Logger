Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = True
'---------------------------------------------------------------------------------------
' Module    : clsLogFilterCollection
' Author    : K.Gundermann
' Date      : 04.02.2012
' Purpose   : Collection of Matchers
'             MatchFilter is True as soon as ONE Matcher is True
'---------------------------------------------------------------------------------------

Option Compare Database
Option Explicit

Private colFilterCollection  As Collection


Private Sub Class_Initialize()
  Set colFilterCollection = New Collection
End Sub

Public Sub AddFilter(ByVal TheFilter As clsLogFilter)
  Collection.Add TheFilter
End Sub

Public Property Get Collection() As Collection
  Set Collection = colFilterCollection
End Property

Public Property Get Count() As Long
  Count = Collection.Count
End Property

Public Function MatchFilter(objLogEntry As clsLogEntry) As Boolean
  Dim varFilter As Variant
  Dim objFilter As clsLogFilter
  
  If Collection.Count = 0 Then
    MatchFilter = True
  Else
    For Each varFilter In Collection
      Set objFilter = varFilter
      If objFilter.IsMatch(objLogEntry) Then
        MatchFilter = True
        Exit Function
      End If
    Next
  End If
End Function

Public Sub Clear()
  Call Class_Initialize
End Sub